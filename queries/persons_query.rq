PREFIX rdf:      <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs:     <http://www.w3.org/2000/01/rdf-schema#>
PREFIX xsd:      <http://www.w3.org/2001/XMLSchema#>
PREFIX owl:      <http://www.w3.org/2002/07/owl#>
PREFIX swrl:     <http://www.w3.org/2003/11/swrl#>
PREFIX swrlb:    <http://www.w3.org/2003/11/swrlb#>
PREFIX vitro:    <http://vitro.mannlib.cornell.edu/ns/vitro/0.7#>
PREFIX bibo:     <http://purl.org/ontology/bibo/>
PREFIX c4o:      <http://purl.org/spar/c4o/>
PREFIX cito:     <http://purl.org/spar/cito/>
PREFIX event:    <http://purl.org/NET/c4dm/event.owl#>
PREFIX fabio:    <http://purl.org/spar/fabio/>
PREFIX foaf:     <http://xmlns.com/foaf/0.1/>
PREFIX geo:      <http://aims.fao.org/aos/geopolitical.owl#>
PREFIX obo:      <http://purl.obolibrary.org/obo/>
PREFIX ocrer:    <http://purl.org/net/OCRe/research.owl#>
PREFIX ocresd:   <http://purl.org/net/OCRe/study_design.owl#>
PREFIX skos:     <http://www.w3.org/2004/02/skos/core#>
PREFIX tib:      <http://vivo.mydomain.edu/ontology/tib-local#>
PREFIX vcard:    <http://www.w3.org/2006/vcard/ns#>
PREFIX vitro-public: <http://vitro.mannlib.cornell.edu/ns/vitro/public#>
PREFIX vivo:     <http://vivoweb.org/ontology/core#>
PREFIX scires:   <http://vivoweb.org/ontology/scientific-research#>


SELECT (?author_id AS ?id) (?name AS ?fullname) (?author_id AS ?uri) (GROUP_CONCAT(DISTINCT ?gnd_id; separator="; ") AS ?gnd) (?bytestreamUri AS ?image) (GROUP_CONCAT(DISTINCT ?org_label; separator="; ") AS ?positions) ?name (COUNT(DISTINCT ?document_id) AS ?count) (GROUP_CONCAT(DISTINCT ?project_id; separator="; ") AS ?projects) (GROUP_CONCAT(DISTINCT ?document_id; separator="; ") AS ?publications)
WHERE
{
  ?author_id a foaf:Person .
  ?author_id rdfs:label ?name .
  OPTIONAL {
    ?author_id vitro-public:mainImage ?picture .
    ?picture vitro-public:downloadLocation ?bytestreamUri .
  }
  OPTIONAL {
    ?author_id tib:gndId ?gnd_id .
  }
  OPTIONAL {
    ?pos a vivo:Position .
    ?author_id vivo:relatedBy ?pos .
    ?org a foaf:Organization .
    ?pos vivo:relates ?org .
    ?org rdfs:label ?org_label .
  }
  ?authorship_id a vivo:Authorship .
  ?authorship_id vivo:relates ?author_id .
  ?document_id a bibo:Document .
  ?document_id vivo:relatedBy ?authorship_id .
  OPTIONAL {
    ?project_id a vivo:Grant .
    ?author_id vivo:relatedBy ?project_id .
  }
}
GROUP BY ?author_id ?bytestreamUri ?name
ORDER BY ?name