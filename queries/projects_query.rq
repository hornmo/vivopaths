PREFIX rdf:      <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs:     <http://www.w3.org/2000/01/rdf-schema#>
PREFIX xsd:      <http://www.w3.org/2001/XMLSchema#>
PREFIX owl:      <http://www.w3.org/2002/07/owl#>
PREFIX swrl:     <http://www.w3.org/2003/11/swrl#>
PREFIX swrlb:    <http://www.w3.org/2003/11/swrlb#>
PREFIX vitro:    <http://vitro.mannlib.cornell.edu/ns/vitro/0.7#>
PREFIX bibo:     <http://purl.org/ontology/bibo/>
PREFIX c4o:      <http://purl.org/spar/c4o/>
PREFIX cito:     <http://purl.org/spar/cito/>
PREFIX event:    <http://purl.org/NET/c4dm/event.owl#>
PREFIX fabio:    <http://purl.org/spar/fabio/>
PREFIX foaf:     <http://xmlns.com/foaf/0.1/>
PREFIX geo:      <http://aims.fao.org/aos/geopolitical.owl#>
PREFIX obo:      <http://purl.obolibrary.org/obo/>
PREFIX ocrer:    <http://purl.org/net/OCRe/research.owl#>
PREFIX ocresd:   <http://purl.org/net/OCRe/study_design.owl#>
PREFIX skos:     <http://www.w3.org/2004/02/skos/core#>
PREFIX tib:      <http://vivo.mydomain.edu/ontology/tib-local#>
PREFIX vcard:    <http://www.w3.org/2006/vcard/ns#>
PREFIX vitro-public: <http://vitro.mannlib.cornell.edu/ns/vitro/public#>
PREFIX vivo:     <http://vivoweb.org/ontology/core#>
PREFIX scires:   <http://vivoweb.org/ontology/scientific-research#>


SELECT DISTINCT (?grant_id AS ?id) ?title (?grant_id AS ?uri) (GROUP_CONCAT(DISTINCT ?document_id; separator="; ") AS ?publications) (COUNT(DISTINCT ?document_id) AS ?count) (GROUP_CONCAT(DISTINCT ?keyword; separator="; ") AS ?keywords) (GROUP_CONCAT(DISTINCT ?inv_id; separator="; ")AS ?investigators) ?start ?end ?abstract ?principal_investigator
WHERE
{
  ?grant_id a vivo:Grant .
  ?grant_id rdfs:label ?title .
  OPTIONAL { ?grant_id bibo:abstract ?abstract }
  OPTIONAL {
    ?document_id a bibo:Document .
    ?grant_id vivo:supportedInformationResource ?document_id .
  }
  OPTIONAL { ?grant_id vivo:hasSubjectArea ?keyword }
  ?grant_id vivo:dateTimeInterval ?dateInterval .
  ?dateInterval vivo:start ?startInd .
  ?dateInterval vivo:end ?endInd .
  ?startInd vivo:dateTime ?start .
  ?endInd vivo:dateTime ?end .
  OPTIONAL{
    ?principal_investigator a vivo:PrincipalInvestigatorRole .
    ?grant_id  vivo:relatedBy ?principal_investigator . 
  }
  OPTIONAL { 
    ?inv_id a foaf:Person .
    ?inv_id vivo:relatedBy ?grant_id .
  }
}
GROUP BY ?grant_id ?title ?abstract ?start ?end ?principal_investigator
ORDER BY ?title